
machine WaterBoiler {

  event button_on  "button 1 pressed";
  event button_start  "button 2 pressed";

  internal event tick "test if glass full";

  Stopwatch on_time;
  Boiler boiler;
  Pump pump;

  initial state bootup {
    entry {
      on_time.init();
      transition suspend;
    }
  }
  
  state suspend {
    entry {
      on_time.stop();
    }

    event button_start
    {
        // ignore event
    }

    event button_on
    {
      transition on;
    }
  }
  
  
  state on {
    entry {
      on_time.start();
    }
    
    event button_on
    {      
      transition suspend;
    }

    event button_start {
      boiler.on();
      pump.on();

      transition brewing::warmup;
    }
  }

  state brewing::warmup {
    int warmup_steps;
    entry {
      warmup_steps = 0;
      after secs(1) emit tick;      
    }

    event button_start
    {
      // ignore event
    }

    event button_on
    {
        // ignore event
    }


    event tick {
      warmup_steps += 1;
      if (warmup_steps < 4) {
	after secs(1) emit tick;
      } else {
	transition brewing::boiling;
      }
    }
  }
  
  state brewing::boiling {
    int boil_steps;
    
    entry {
       boil_steps = 0;
       after secs(1) emit tick;
    }

    event button_start
    {
      // ignore event
    }

    event button_on
    {
        // ignore event
    }

    event tick {
      if (boiler.done()) {
	printf("boiler has finished\n");
	transition brewing::finish_boiling;
      } else {
	printf("boiler not done yet\n");
	after secs(1) emit tick;
      }
    }
  }

  state brewing::finish_boiling {
    entry {
      boiler.off();
      pump.off();
      transition on;
    }
  }
}

#include "tests.boiler.sm"
